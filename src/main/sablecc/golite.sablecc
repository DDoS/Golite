Package golite ;

/*
TODO: fix these lexer bugs

6. Automatic semicolon insertion


TODO: weeding of

1. Assignment LHS
4. Break and continue
5. Return? Need to ask prof
*/

Helpers

    // CHARACTERS

    // Some Symbols
    underscore = '_';
    slash = '/';
    backslash = '\';
    star = '*';
    dot = '.';

    // Universal Character Set
    ch_set = [0 .. 0xFFFF];

    tab = 9;
    newln = 10 ;
    carriage = 13;

    endln = carriage newln | carriage | newln;
    not_endln = [ch_set - [carriage + newln]];

    // Alphabets
    alpha = ['A' .. 'Z'] | ['a' .. 'z'];

    // Numeric Characters
    zero = '0';
    num_ch = ['0' .. '9'] ;
    oct_num_ch = ['0' .. '7'];

    nozero = [num_ch - '0'];
    hex_num_ch = num_ch | alpha;

    sp = ' ';
    single_quot = ''';
    double_quot = '"';
    back_q = '`';

    // Escape Sequence
    escapes = '\' ('\'| 'a' | 'b' | 'f' | 'n' | 'r' | 't' | 'v' );
    escape_squote = '\' ''';
    escape_dquote = '\"';

    no_carr_nl = [ch_set - [carriage + newln]] ;
    no_carr_nl_sl = [no_carr_nl - backslash];

    letter = alpha | underscore;
    alpha_num = letter | num_ch ;

Tokens

    star = star;
    slash = slash;

    // WHITESPACE
    wht_sp = (sp | endln | tab) +;

    // KEYWORDS
    // Go keywords
    kw_break = 'break';
    kw_case = 'case';
    kw_chan = 'chan';
    kw_const = 'const';
    kw_continue = 'continue';
    kw_default = 'default';
    kw_defer = 'defer';
    kw_else = 'else';
    kw_fallthrough = 'fallthrough';
    kw_for = 'for';
    kw_func = 'func';
    kw_go = 'go';
    kw_goto = 'goto';
    kw_if = 'if';
    kw_import = 'import';
    kw_interface = 'interface';
    kw_map = 'map';
    kw_package = 'package';
    kw_range = 'range';
    kw_return = 'return';
    kw_select = 'select';
    kw_struct = 'struct';
    kw_switch = 'switch';
    kw_type = 'type';
    kw_var = 'var';

    // Golite additional keywords
    print = 'print';
    println = 'println';
    append = 'append';

    //Operators
    plus = '+';
    minus = '-';

    mod = '%';
    ampr = '&';
    pipe = '|';
    crt = '^';
    lshift = '<<';
    rshift = '>>';
    ampr_crt = '&^';
    plus_eql = '+=';
    minus_eql = '-=';
    star_eql = '*=';
    slash_eql = '/=';
    mod_eql = '%=';
    ampr_eql = '&=';
    pipe_eql = '|=';
    crt_eql = '^=';
    lshift_eql = '<<=';
    rshift_eql = '>>=';
    ampr_crt_eql = '&^=';
    dbl_ampr = '&&';
    dbl_pipe = '||';
    inp = '<-';
    dbl_plus = '++';
    dbl_minus = '--';
    dbl_eql = '==';
    less_than = '<';
    greatr_than = '>';
    eql = '=';
    not = '!';
    not_eql = '!=';
    less_than_eql = '<=';
    greatr_than_eql = '>=';
    col_eql = ':=';
    ellipsis = '...';

    lf_paren = '(';
    lf_sq_paren = '[';
    lf_brace = '{';
    comma = ',';
    dot = dot;

    rht_paren = ')';
    rht_sq_paren = ']';
    rht_brace = '}';

    semi = ';';
    coln = ':';

    // COMMENTS
    comment = '//' not_endln* | '/*' ([ch_set - star] | star [ch_set - slash])* '*/';

    // IDENTIFIERS
    idenf = letter alpha_num*;

    // LITERALS
    // Int
    int_lit = zero | num_ch num_ch*;
    oct_lit = zero oct_num_ch+;
    hex_lit = zero ['x' + 'X'] hex_num_ch+;

    // Float
    float_lit = num_ch+ dot num_ch* | dot num_ch+;

    // String
    // "hello\n" // Interpreted string, \n is transformed into newline
    interpreted_string_lit = double_quot ([no_carr_nl_sl - single_quot] | escape_dquote | escapes)* double_quot;

    // `hello\n` // Raw string, \n appears as a `\` followed by `n`
    raw_string_lit = back_q [ch_set - back_q]* back_q;

    // Rune
    rune_lit = single_quot ([no_carr_nl_sl - single_quot] | escape_squote | escapes) single_quot;

Ignored Tokens
    comment,
    wht_sp;

Productions

    // JUST FOR TESTING
    start {-> expr} = expr {-> expr.expr};

    // Program
    prog = decl_pack decl*;

    // DECLARATIONS
    decl = {var} decl_var | {type} decl_type | {func} decl_func;

    // Type and variable declarations are also statements
    stmt_decl = {var} decl_var
            | {type} decl_type;

    // Package declaration
    decl_pack = kw_package idenf semi;

    // Variable declarations
    decl_var = {single} kw_var spec_var_semi
            | {distributed} kw_var lf_paren spec_var_semi* rht_paren semi;

    decl_var_short = {short} idenf_list col_eql exprs_list;

    spec_var_semi = spec_var semi;
    spec_var = {type} idenf_list type
            | {expr} idenf_list eql exprs_list
            | {type_and_expr} idenf_list type eql exprs_list;

    // A list of identifiers. The blank identifier isn't special in the syntax!
    idenf_list {-> idenf*} = {single} idenf
                    {-> [idenf]}
            | {multiple} idenf comma idenf_list
                    {-> [idenf, idenf_list.idenf]};

    // Type declarations
    decl_type = {single} kw_type field_type_semi
            | {distributed} kw_type lf_paren field_type_semi* rht_paren semi;

    // A field in a type declaration, ending in a semicolon
    field_type_semi = idenf type semi;

    // Function declarations
    decl_func = {return} kw_func idenf lf_paren param_list rht_paren type block semi
            | {no_return} kw_func idenf lf_paren param_list rht_paren block semi;

    param_list = {empty} | {param_list} param_list_element* param_spec;
    param_list_element = param_spec comma;
    param_spec = idenf_list type;

    block = lf_brace stmt_list* rht_brace;

    // STATEMENTS
    stmt_list = stmt semi;

    stmt = {empty}
            | simple_stmt
            | {print} print lf_paren exprs_list rht_paren
            | {println} println lf_paren exprs_list rht_paren
            | {return} kw_return
            | {return_exp} kw_return expr
            | {if} kw_if if_cond block else
            | {switch} kw_switch optional_switch lf_brace switch_case* rht_brace
            | {for} kw_for optional_for block;

    if_cond = {stmt} simple_stmt semi
            | {expr} expr
            | {stmt_expr} simple_stmt semi expr;

    else = {else} kw_else block
            | {elseif} kw_else kw_if expr block
            | {empty};

    simple_stmt = {empty}
            | {expr} expr
            | {incr} expr dbl_plus
            | {decr} expr dbl_minus
            | {short_decl} decl_var_short
            | {assign} [left]:exprs_list eql [right]:exprs_list;

    optional_switch = {stmt} simple_stmt semi
            | {expr} expr
            | {stmt_exp} simple_stmt semi expr;

    switch_case = {clause} expr_switch_case coln stmt_list*;

    expr_switch_case = {exprs} kw_case exprs_list | {default} kw_default;

    optional_for = {empty}
            | {condition} expr
            |  {clause} [init]:for_stmt [cond]:for_stmt post_stmt;

    for_stmt = {stmt} simple_stmt semi| {empty} semi;

    post_stmt = {stmt} simple_stmt | {empty};

    // EXPRESSIONS
    expr {-> expr} = expr_binary_one {-> expr_binary_one.expr};

    expr_int {-> expr} = {int_dec} int_lit
                    {-> New expr.int_dec(int_lit)}
            | {int_octal} oct_lit
                    {-> New expr.int_oct(oct_lit)}
            | {int_hex} hex_lit
                    {-> New expr.int_hex(hex_lit)};

    expr_string {-> expr} = {string_intr} interpreted_string_lit
                    {-> New expr.string_intr(interpreted_string_lit)}
            | {string_raw} raw_string_lit
                    {-> New expr.string_raw(raw_string_lit)};

    expr_atom {-> expr} = {ident} idenf
                    {-> New expr.ident(idenf)}
            | {int} expr_int
                    {-> expr_int.expr}
            | {float} float_lit
                    {-> New expr.float(float_lit)}
            | {rune} rune_lit
                    {-> New expr.rune(rune_lit)}
            | {string} expr_string
                    {-> expr_string.expr}
            | {paren} lf_paren expr rht_paren
                    {-> expr.expr};

    // Arguments are comma separated
    exprs_list {-> expr*} = {single} expr
                    {-> [expr.expr]}
            | {multiple} expr comma exprs_list
                    {-> [expr.expr, exprs_list.expr]};
    // An argument list is between () and can be empty
    args_list {-> expr*} = lf_paren exprs_list? rht_paren
                    {-> [exprs_list.expr]};

    expr_primary {-> expr} = {atom} expr_atom
                    {-> expr_atom.expr}
            | {select} expr_primary dot idenf
                    {-> New expr.select(expr_primary.expr, idenf)}
            | {index} expr_primary lf_sq_paren expr rht_sq_paren
                    {-> New expr.index(expr_primary.expr, expr.expr)}
            | {call} expr_primary args_list
                    {-> New expr.call(expr_primary.expr, [args_list.expr])}
            | {append} append lf_paren [arg1]:expr comma [arg2]:expr rht_paren
                    {-> New expr.append(arg1.expr, arg2.expr)};

    expr_unary {-> expr} = {primary} expr_primary
                    {-> expr_primary.expr}
            | {logic_not} not expr_unary
                    {-> New expr.logic_not(expr_unary.expr)}
            | {reaffirm} plus expr_unary
                    {-> New expr.reaffirm(expr_unary.expr)}
            | {negate} minus expr_unary
                    {-> New expr.negate(expr_unary.expr)}
            | {bit_not} crt expr_unary
                    {-> New expr.bit_not(expr_unary.expr)};

    expr_binary_five {-> expr} = {unary} expr_unary
                    {-> expr_unary.expr}
            | {mul} expr_binary_five star expr_unary
                    {-> New expr.mul(expr_binary_five.expr, expr_unary.expr)}
            | {div} expr_binary_five slash expr_unary
                    {-> New expr.div(expr_binary_five.expr, expr_unary.expr)}
            | {rem} expr_binary_five mod expr_unary
                    {-> New expr.rem(expr_binary_five.expr, expr_unary.expr)}
            | {lshift} expr_binary_five lshift expr_unary
                    {-> New expr.lshift(expr_binary_five.expr, expr_unary.expr)}
            | {rshift} expr_binary_five rshift expr_unary
                    {-> New expr.rshift(expr_binary_five.expr, expr_unary.expr)}
            | {bit_and} expr_binary_five ampr expr_unary
                    {-> New expr.bit_and(expr_binary_five.expr, expr_unary.expr)}
            | {bit_and_not} expr_binary_five ampr_crt expr_unary
                    {-> New expr.bit_and_not(expr_binary_five.expr, expr_unary.expr)};

    expr_binary_four {-> expr} = {binary_five} expr_binary_five
                    {-> expr_binary_five.expr}
            | {add} expr_binary_four plus expr_binary_five
                    {-> New expr.add(expr_binary_four.expr, expr_binary_five.expr)}
            | {sub} expr_binary_four minus expr_binary_five
                    {-> New expr.sub(expr_binary_four.expr, expr_binary_five.expr)}
            | {bit_or} expr_binary_four pipe expr_binary_five
                    {-> New expr.bit_or(expr_binary_four.expr, expr_binary_five.expr)}
            | {bit_xor} expr_binary_four crt expr_binary_five
                    {-> New expr.bit_xor(expr_binary_four.expr, expr_binary_five.expr)};

    expr_binary_three {-> expr} = {binary_four} expr_binary_four
                    {-> expr_binary_four.expr}
            | {eq} expr_binary_three dbl_eql expr_binary_four
                    {-> New expr.eq(expr_binary_three.expr, expr_binary_four.expr)}
            | {neq} expr_binary_three not_eql expr_binary_four
                    {-> New expr.neq(expr_binary_three.expr, expr_binary_four.expr)}
            | {less} expr_binary_three less_than expr_binary_four
                    {-> New expr.less(expr_binary_three.expr, expr_binary_four.expr)}
            | {less_eq} expr_binary_three less_than_eql expr_binary_four
                    {-> New expr.less_eq(expr_binary_three.expr, expr_binary_four.expr)}
            | {great} expr_binary_three greatr_than expr_binary_four
                    {-> New expr.great(expr_binary_three.expr, expr_binary_four.expr)}
            | {great_eq} expr_binary_three greatr_than_eql expr_binary_four
                    {-> New expr.great_eq(expr_binary_three.expr, expr_binary_four.expr)};

    expr_binary_two {-> expr} = {binary_three} expr_binary_three
                    {-> expr_binary_three.expr}
            | {logic_and} expr_binary_two dbl_ampr expr_binary_three
                    {-> New expr.logic_and(expr_binary_two.expr, expr_binary_three.expr)};

    expr_binary_one {-> expr} = {binary_two} expr_binary_two
                    {-> expr_binary_two.expr}
            | {logic_or} expr_binary_one dbl_pipe expr_binary_two
                    {-> New expr.logic_or(expr_binary_one.expr, expr_binary_two.expr)};

    // TYPES
    type {-> type} = {basic} type_name
                    {-> type_name.type}
            | {slice} type_slice
                    {-> type_slice.type}
            | {array} type_array
                    {-> type_array.type}
            | {struct} type_struct
                    {-> type_struct.type};

    type_name {-> type} = idenf
                    {-> New type.name(idenf)};

    type_slice {-> type} = lf_sq_paren rht_sq_paren type
                    {-> New type.slice(type.type)};

    type_array {-> type} = lf_sq_paren expr_int rht_sq_paren type
                    {-> New type.array(type.type, expr_int.expr)};

    struct_field {-> struct_field} = idenf_list type semi
                    {-> New struct_field(type.type, [idenf_list.idenf])};

    type_struct {-> type} = kw_struct lf_brace struct_field* rht_brace
                    {-> New type.struct([struct_field.struct_field])};

Abstract Syntax Tree

    // EXPRESSIONS
    expr = {ident} idenf
            | {int_dec} int_lit
            | {int_oct} oct_lit
            | {int_hex} hex_lit
            | {float} float_lit
            | {rune} rune_lit
            | {string_intr} interpreted_string_lit
            | {string_raw} raw_string_lit
            | {select} [value]:expr idenf
            | {index} [value]:expr [index]:expr
            | {call} [value]:expr [args]:expr*
            | {cast} [type]:type [value]:expr
            | {append} [left]:expr [right]:expr
            | {logic_not} [inner]:expr
            | {reaffirm} [inner]:expr
            | {negate} [inner]:expr
            | {bit_not} [inner]:expr
            | {mul} [left]:expr [right]:expr
            | {div} [left]:expr [right]:expr
            | {rem} [left]:expr [right]:expr
            | {lshift} [left]:expr [right]:expr
            | {rshift} [left]:expr [right]:expr
            | {bit_and} [left]:expr [right]:expr
            | {bit_and_not} [left]:expr [right]:expr
            | {add} [left]:expr [right]:expr
            | {sub} [left]:expr [right]:expr
            | {bit_or} [left]:expr [right]:expr
            | {bit_xor} [left]:expr [right]:expr
            | {eq} [left]:expr [right]:expr
            | {neq} [left]:expr [right]:expr
            | {less} [left]:expr [right]:expr
            | {less_eq} [left]:expr [right]:expr
            | {great} [left]:expr [right]:expr
            | {great_eq} [left]:expr [right]:expr
            | {logic_and} [left]:expr [right]:expr
            | {logic_or} [left]:expr [right]:expr;

    // TYPES
    type = {name} idenf
            | {slice} type
            | {array} type expr
            | {struct} [fields]:struct_field*;

    struct_field = type [names]:idenf*;
